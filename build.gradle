plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id 'maven-publish'
}

group = 'com.mengcraft'

allprojects {
    version = "2.10-SNAPSHOT"

    apply plugin: 'java'

    repositories {
        mavenLocal()
        maven {
            url 'http://ci.mengcraft.com:8081/plugin/repository/everything/'
        }
        mavenCentral()
        jcenter()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.2'
    }
}

project(":internal") {
    dependencies {
        compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    }

    subprojects {
        group = 'com.mengcraft.playersql'
        dependencies {
            compileOnly project(":internal")
        }
    }
}

dependencies {
    compile subprojects
    compileOnly 'org.lz4:lz4-java:1.5.0'
    compileOnly 'net.md-5:bungeecord-api:1.12-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    compileOnly 'com.mengcraft:simpleorm:1.2-SNAPSHOT'
    compileOnly 'org.avaje:ebean:2.8.1'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.4.0-SNAPSHOT'
}

jar.finalizedBy(shadowJar)

publishing {
    publications {
        maven(MavenPublication) {
            project.shadow.component(it)
        }
    }
    repositories {
        maven {
            url = "${DEPLOY_REPOSITORY}"
            credentials {
                username = "${DEPLOY_USER}"
                password = "${DEPLOY_PASSWORD}"
            }
        }
    }
}
